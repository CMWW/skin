plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    compileSdk rootProject.compileSdkVersion

    defaultConfig {
        minSdk rootProject.minSdkVersion
        targetSdk rootProject.targetSdkVersion
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    namespace 'skin.support'

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

tasks.withType(Javadoc).configureEach {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

ext {
    publishVersion = rootProject.skinAndroidXVersion
    publishGroup = rootProject.skinAndroidXSkinGroup
    moduleName = 'skin-support'
    descContent = 'skin-support core'
}
tasks.register('sourceJar', Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.findByName('release')

                // You can then customize attributes of the publication as shown below.
                groupId = 'com.github.CMWW'
                artifactId = 'skin-support'
                version = publishVersion
                artifact(sourceJar)
            }
        }
    }
}