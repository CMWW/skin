plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    compileSdk rootProject.compileSdkVersion

    defaultConfig {
        minSdk rootProject.minSdkVersion
        targetSdk rootProject.targetSdkVersion
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            res.srcDirs = ['src/main/res', 'src/main/res-alertdialog']
        }
    }
    namespace 'skin.support.appcompat'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation project(':androidx:skin-support')
}

tasks.withType(Javadoc).configureEach {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

ext {
    publishVersion = rootProject.skinAndroidXVersion
    publishGroup = rootProject.skinAndroidXSkinGroup
    moduleName = 'skin-support-appcompat'
    descContent = 'skin-support 基础控件换肤支持包'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.findByName('release')
                groupId = 'com.github.littlefogcat'
                artifactId = 'skin-support-appcompat'
                version = rootProject.skinAndroidXVersion
            }
        }
    }
}